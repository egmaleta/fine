program : stmt_list
        | <empty>

stmt_list : stmt stmt_list
          | stmt

stmt : val_defn
     | fun_defn
     | binop_info

val_defn : VAL name ASSIGN expr

fun_defn : FUN name params ASSIGN expr
         | FUN ID operator ID ASSIGN expr

binop_info : INFIXL NAT operator
           | INFIXR NAT operator

params : ID params
       | ID

expr : op_chain

op_chain : operand operator op_chain
         | operand

operand : atom args

atom : OPAR expr CPAR
     | name
     | literal

name : ID
     | OPAR EXT_OP CPAR
     | OPAR OP CPAR

literal : DEC
        | NAT
        | UNIT

operator : EXT_OP
         | OP
         | BTICK ID BTICK

args : ID ASSIGN atom args
     | atom args
     | <empty>
