program : stmt_list
        | <empty>

stmt_list : stmt NEWLINE stmt_list
          | stmt DEDENT stmt_list
          | stmt

opt_ind_expr : INDENT expr
             | expr

stmt : val_defn
     | fun_defn
     | binop_info

val_defn : name ASSIGN opt_ind_expr

fun_defn : name params ASSIGN opt_ind_expr
         | ID operator ID ASSIGN opt_ind_expr

binop_info : INFIXL NAT op_repr
           | INFIXR NAT op_repr

op_repr : operator
        | OPAR operator CPAR

name : ID
     | OPAR operator CPAR

params : ID params
       | ID

expr : op_chain

op_chain : operand operator op_chain
         | operand

operand : atom args

atom : OPAR expr CPAR
     | OPAR operator CPAR
     | ID
     | literal

literal : DEC
        | NAT
        | UNIT

operator : EXT_OP
         | OP

args : ID ASSIGN atom args
     | atom args
     | <empty>
